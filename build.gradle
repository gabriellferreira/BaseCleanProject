buildscript {
    ext.kotlin_version = '1.2.60'
    ext.detektVersion = "1.0.0.RC8"

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
    }
}

plugins {
    id "nebula.lint" version "9.3.1"
}

gradleLint {
    rules  = ['dependency-parentheses', 'duplicate-dependency-class', 'unused-exclude-by-dep', 'unused-exclude-by-conf']
    criticalRules = ['unused-dependency'] // <-- this will fail the build in the event of a violation
}

apply plugin: "io.gitlab.arturbosch.detekt"

detekt {
    version = detektVersion
    profile("main") {
        input = "$projectDir/app"
        config = "$projectDir/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        parallel = true
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    apply plugin: 'nebula.lint'
}

ext {
    mvlVersionMajor = 1   // 100.000
    mvlVersionMinor = 0   //   1.000
    mvlVersionPatch = 0   //     100

    compileSdkVersion = 27
    targetSdkVersion = 27
    buildToolsVersion = "27.0.3"
    supportLibraryVersion = "27.1.1"

    versionCode = mvlVersionMajor * 100000 + mvlVersionMinor * 1000 + mvlVersionPatch * 10
    versionName = "$mvlVersionMajor.$mvlVersionMinor.$mvlVersionPatch"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
